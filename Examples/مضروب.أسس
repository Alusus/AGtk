اشمل "مـحا.أسس"؛
مـحا.اشمل_ملف("Alusus/Gtk", "جـتك.أسس")؛
اشمل "بـناء.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـتم/نـص.أسس"؛

@تصدير[exit] دالة اخرج(صحيح)؛

وحدة مـضروب {
    دالة مضروب (م: صحيح): صحيح[64] {
        إذا م <= 0 أرجع 1 وإلا أرجع م * مضروب(م - 1)؛
    }؛
    
    دالة اختبر_المضروب (): ثـنائي {
        إذا مضروب(5) != 120 أرجع 0؛
        إذا مضروب(0) != 1 أرجع 0؛
        أرجع 1؛
    }؛
}؛

وحدة واجـهة {
    عرف النافذة: سند[جـتك.نـافذة_تطبيق]؛

    @تصدير[main]
    دالة ابدأ {
        عرف التطبيق: سند[جـتك.تـطبيق]؛
        عرف الحالة: صحيح؛
        عرف معرف_التطبيق: "org.alusus.gtk-factorial"؛
        التطبيق~مؤشر = جـتك.تـطبيق.أنشئ(معرف_التطبيق، 0)~مؤشر؛
        التطبيق.اربط_إشارة(جـتك.إشـارة._تفعيل_، جـلب.رد[فعّل]، 0، 0، 0)؛
        الحالة = التطبيق.شغل(0، 0)؛
        جـلب.حرر(التطبيق)؛
    }؛

    دالة فعّل (تطبيق: سند[جـتك.تـطبيق]، معلومات: مؤشر) {
        عرف صندوق: سند[جـتك.صـندوق]؛
        عرف لصيقة: سند[جـتك.لـصيقة]؛
        عرف حقل: سند[جـتك.حـقل]؛
        عرف صندوق_الأزرار: سند[جـتك.صـندوق_أزرار]؛
        عرف زر_حول: سند[جـتك.الـزر]؛
        عرف زر_الخروج: سند[جـتك.الـزر]؛

        النافذة~مؤشر = جـتك.نـافذة_تطبيق.أنشئ(تطبيق)~مؤشر؛
        النافذة.عين_عنوانا("الأسس/وحدة جـتك: المضروب")؛
        النافذة.عين_الحجم_المبدئي(480، 320)؛

        صندوق~مؤشر = جـتك.صـندوق.أنشئ(جـتك.اتـجاه._عمودي_، 20)~مؤشر؛

        لصيقة~مؤشر = جـتك.لـصيقة.أنشئ("بسم الله الرحمن الرحيم")~مؤشر؛

        حقل~مؤشر = جـتك.حـقل.أنشئ()~مؤشر؛
        حقل.اربط_إشارة(جـتك.إشـارة._تغير_، جـلب.رد[قد_تغير_الحقل]، لصيقة~مؤشر، 0، 0)؛

        صندوق_الأزرار~مؤشر = جـتك.صـندوق_أزرار.أنشئ(جـتك.اتـجاه._أفقي_)~مؤشر؛
        صندوق_الأزرار.عين_النسق(جـتك.طـراز_صندوق_الأزرار._انتشار_)؛

        زر_حول~مؤشر = جـتك.الـزر.أنشئ_بلصيقة("حول")~مؤشر؛
        زر_حول.اربط_إشارة(جـتك.إشـارة._نقر_، جـلب.رد[قد_نقر_زر_حول]، 0، 0، 0)؛
        صندوق_الأزرار.أضف(زر_حول)؛

        زر_الخروج~مؤشر = جـتك.الـزر.أنشئ_بلصيقة("خروج")~مؤشر؛
        زر_الخروج.اربط_إشارة(جـتك.إشـارة._نقر_، جـلب.رد[قد_نقر_زر_الخروج]، 0، 0، 0)؛
        صندوق_الأزرار.أضف(زر_الخروج)؛

        صندوق.احشد_في_البداية(لصيقة، 1، 0، 0)؛
        صندوق.احشد_في_البداية(حقل، 1، 0، 0)؛
        صندوق.احشد_في_البداية(صندوق_الأزرار، 1، 0، 0)؛

        النافذة.أضف(صندوق)؛

        النافذة.أظهر_الكل()؛
    }؛

    دالة قد_تغير_الحقل (قابل_للتعديل: سند[جـتك.قـابل_للتعديل]، لصيقة: سند[جـتك.لـصيقة]) {
        عرف معطى: صـحيح؛
        إذا مـتم.نـص.امسح(جـتك.قـابل_للتعديل.هات_محارف(قابل_للتعديل، 0، -1)، "%d"، معطى~مؤشر) != 0 {
            عرف محارف: مصفوفة[محرف، 100]؛
            مـتم.نـص.عين(محارف~مؤشر، "مضروب %ld = %ld"، معطى، مـضروب.مضروب(معطى))؛
            لصيقة.عين_النص(محارف~مؤشر)؛
        }؛
    }؛

    دالة قد_نقر_زر_حول (عنصر: سند[جـتك.ودجـة]، معلومات: مؤشر) {
        عرف حوارية: سند[جـتك.حـوارية]؛
        حوارية~مؤشر = جـتك.حـوارية_رسالة.أنشئ(
            النافذة، 0، جـتك.صـنف_رسالة._معلومة_، جـتك.نـوع_الزر._أغلق_، "المضروب.\nتطوير: سرمد خالد عبد الله"
        )~مؤشر؛
        حوارية.شغل()؛
        جـتك.ودجـة.مزق(حوارية)؛
    }؛

    دالة قد_نقر_زر_الخروج (عنصر: سند[جـتك.ودجـة]، معلومات: مؤشر) {
        جـتك.ودجـة.مزق(النافذة)؛
    }؛
}؛


إذا الـعملية.عدد_المعطيات < 3 {
    // تشغيل واجهة المستخدم
    واجـهة.ابدأ()؛
} وإلا {
    عرف معطى: صـحيح؛
    إذا مـتم.نـص.امسح(الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1)، "%d"، معطى~مؤشر) != 0 {
        // طباعة النتيجة مباشرة
        مـتم.طـرفية.اطبع("مضروب %ld = %ld\ج"، معطى، مـضروب.مضروب(معطى))؛
    } وإلا {
        // تنفيذ الاختبارات الآلية
        إذا مـضروب.اختبر_المضروب() == 0 {
            مـتم.طـرفية.اطبع("فشلت الاختبارات الآلية.\ج")؛
            اخرج(1)؛
        }
        // بناء ملف تنفيذي
        عرف تنفيذي: بـناء.تـنفيذي(واجـهة.ابدأ~شبم، الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1))؛
        تنفيذي.أضف_اعتمادية(جـتك~شبم)؛
        إذا تنفيذي.أنتج() {
            مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
        } وإلا {
            مـتم.طـرفية.اطبع("فشل البناء.\ج")؛
            اخرج(1)؛
        }؛
    }؛
}؛

