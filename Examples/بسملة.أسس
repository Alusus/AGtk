اشمل "مـحا.أسس"؛
مـحا.اشمل_ملف("Alusus/Gtk", "جـتك.أسس")؛
اشمل "بـناء.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛

استخدم جـتك؛

وحدة الـبسملة {
  عرف النافذة: سند[نـافذة_تطبيق]؛

  @تصدير[main]
  دالة ابدأ {
    عرف التطبيق: سند[تـطبيق]؛
    عرف الحالة: صحيح؛
    عرف معرف_التطبيق: "org.alusus.gtk-basmalah"؛
    التطبيق~مؤشر = تـطبيق.أنشئ(معرف_التطبيق، 0)~مؤشر؛
    التطبيق.اربط_إشارة(إشـارة._تفعيل_، فعّل~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
    الحالة = التطبيق.شغل(0، 0)؛
    جـلب.حرر(التطبيق)؛
  }؛

  دالة فعّل (تطبيق: سند[تـطبيق]، معلومات: مؤشر) {
    عرف صندوق: سند[صـندوق]؛
    عرف لصيقة: سند[لـصيقة]؛
    عرف صندوق_الأزرار: سند[صـندوق_أزرار]؛
    عرف زر_حول: سند[الـزر]؛
    عرف زر_الخروج: سند[الـزر]؛

    النافذة~مؤشر = نـافذة_تطبيق.أنشئ(تطبيق)~مؤشر؛
    النافذة.عين_عنوانا("الأسس/وحدة جـتك: البسملة")؛
    النافذة.عين_الحجم_المبدئي(480، 320)؛

    صندوق~مؤشر = صـندوق.أنشئ(اتـجاه._عمودي_، 20)~مؤشر؛

    لصيقة~مؤشر = لـصيقة.أنشئ("بسم الله الرحمن الرحيم")~مؤشر؛

    صندوق_الأزرار~مؤشر = صـندوق_أزرار.أنشئ(اتـجاه._أفقي_)~مؤشر؛
    صندوق_الأزرار.عين_النسق(طـراز_صندوق_الأزرار._انتشار_)؛

    زر_حول~مؤشر = الـزر.أنشئ_بلصيقة("حول")~مؤشر؛
    زر_حول.اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_حول]، 0، 0، 0)؛
    صندوق_الأزرار.أضف(زر_حول)؛

    زر_الخروج~مؤشر = الـزر.أنشئ_بلصيقة("خروج")~مؤشر؛
    زر_الخروج.اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_الخروج]، 0، 0، 0)؛
    صندوق_الأزرار.أضف(زر_الخروج)؛

    صندوق.احشد_في_البداية(لصيقة، 1، 0، 0)؛
    صندوق.احشد_في_البداية(صندوق_الأزرار، 1، 0، 0)؛

    النافذة.أضف(صندوق)؛

    النافذة.أظهر_الكل()؛
  }؛

  دالة قد_نقر_زر_حول (عنصر: سند[ودجـة]، معلومات: مؤشر) {
    عرف حوارية: سند[حـوارية]؛
    حوارية~مؤشر = حـوارية_رسالة.أنشئ(
      النافذة، 0، صـنف_رسالة._معلومة_، نـوع_الزر._أغلق_، "البسملة.\nتطوير: سرمد خالد عبد الله"
    )~مؤشر؛
    حوارية.شغل()؛
    ودجـة.مزق(حوارية)؛
  }؛

  دالة قد_نقر_زر_الخروج (عنصر: سند[ودجـة]، معلومات: مؤشر) {
    ودجـة.مزق(النافذة)؛
  }؛
}؛

إذا الـعملية.عدد_المعطيات < 3 {
  الـبسملة.ابدأ()؛
} وإلا {
  عرف تنفيذي: مؤشر[بـناء.تـنفيذي] = بـناء.تـنفيذي.أنشئ(الـبسملة.ابدأ~شبم، الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1))؛
  بـناء.تـنفيذي.أضف_اعتمادية(تنفيذي، جـتك~شبم)؛
  إذا بـناء.تـنفيذي.أنتج(تنفيذي) {
    مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
  } وإلا {
    مـتم.طـرفية.اطبع("فشل البناء.\ج")؛
  }؛
}؛

