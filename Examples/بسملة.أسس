اشمل "مـحا.أسس"؛
مـحا.اشمل_ملف("Alusus/Gtk", "جـتك.أسس")؛
اشمل "بـناء.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛

استخدم جـتك؛

وحدة الـبسملة {
    عرف النافذة: سند[نـافذة_تطبيق]؛

    @تصدير[main]
    دالة ابدأ {
        عرف الحالة: صحيح؛
        عرف معرف_التطبيق: "org.alusus.gtk-basmalah"؛
        عرف التطبيق: سند[تـطبيق](تـطبيق.أنشئ(معرف_التطبيق، 0))؛
        التطبيق.اربط_إشارة(إشـارة._تفعيل_، فعّل~مؤشر~مثل[مؤشر[دالة]]، 0، 0، 0)؛
        الحالة = التطبيق.شغل(0، 0)؛
        جـلب.حرر(التطبيق)؛
    }؛

    دالة فعّل (تطبيق: سند[تـطبيق]، معلومات: مؤشر) {
        النافذة~مؤشر = نـافذة_تطبيق.أنشئ(تطبيق)~مؤشر؛
        النافذة.عين_عنوانا("الأسس/وحدة جـتك: البسملة")؛
        النافذة.عين_الحجم_المبدئي(480، 320)؛

        عرف صندوق: سند[صـندوق](صـندوق.أنشئ(اتـجاه._عمودي_، 20))؛

        عرف لصيقة: سند[لـصيقة](لـصيقة.أنشئ("بسم الله الرحمن الرحيم"))؛

        عرف صندوق_الأزرار: سند[صـندوق_أزرار](صـندوق_أزرار.أنشئ(اتـجاه._أفقي_))؛
        صندوق_الأزرار.عين_النسق(طـراز_صندوق_الأزرار._انتشار_)؛

        عرف زر_حول: سند[الـزر](الـزر.أنشئ_بلصيقة("حول"))؛
        زر_حول.اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_حول]، 0، 0، 0)؛
        صندوق_الأزرار.أضف(زر_حول)؛

        عرف زر_الخروج: سند[الـزر](الـزر.أنشئ_بلصيقة("خروج"))؛
        زر_الخروج.اربط_إشارة(إشـارة._نقر_، جـلب.رد[قد_نقر_زر_الخروج]، 0، 0، 0)؛
        صندوق_الأزرار.أضف(زر_الخروج)؛

        صندوق.احشد_في_البداية(لصيقة، 1، 0، 0)؛
        صندوق.احشد_في_البداية(صندوق_الأزرار، 1، 0، 0)؛

        النافذة.أضف(صندوق)؛

        النافذة.أظهر_الكل()؛
    }؛

    دالة قد_نقر_زر_حول (عنصر: سند[ودجـة]، معلومات: مؤشر) {
        عرف حوارية: سند[حـوارية](حـوارية_رسالة.أنشئ(
           النافذة، 0، صـنف_رسالة._معلومة_، نـوع_الزر._أغلق_، "البسملة.\nتطوير: سرمد خالد عبد الله"
        ))؛
        حوارية.شغل()؛
        ودجـة.مزق(حوارية)؛
    }؛

    دالة قد_نقر_زر_الخروج (عنصر: سند[ودجـة]، معلومات: مؤشر) {
        ودجـة.مزق(النافذة)؛
    }؛
}؛

إذا الـعملية.عدد_المعطيات < 3 {
      الـبسملة.ابدأ()؛
} وإلا {
    عرف تنفيذي: بـناء.تـنفيذي(الـبسملة.ابدأ~شبم، الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1))؛
    تنفيذي.أضف_اعتمادية(جـتك~شبم)؛
    إذا تنفيذي.أنتج() {
        مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
    } وإلا {
        مـتم.طـرفية.اطبع("فشل البناء.\ج")؛
    }؛
}؛

