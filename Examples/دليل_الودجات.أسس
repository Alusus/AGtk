اشمل "مـتم/نـص.أسس"؛
اشمل "مـحا.أسس"؛
 مـحا.اشمل_ملف("Alusus/Gtk", "جـتك.أسس")؛
اشمل "بـناء.أسس"؛
اشمل "مـتم/طـرفية.أسس"؛

وحدة دلـيل_الودجات {
  عرف النافذة: سند[جـتك.نـافذة_تطبيق]؛

  @تصدير[main]
  دالة ابدأ {
    عرف التطبيق: سند[جـتك.تـطبيق]؛
    عرف معرف_التطبيق: "org.alusus.gtk-widget.guide"؛
    التطبيق~مؤشر = جـتك.تـطبيق.أنشئ(معرف_التطبيق، 0)~مؤشر؛
    التطبيق.اربط_إشارة(جـتك.إشـارة._تفعيل_، جـلب.رد[فعّل]، 0، 0، 0)؛
    التطبيق.شغل(0، 0)؛
    جـلب.حرر(التطبيق)؛
  }؛

  دالة فعّل (تطبيق: سند[جـتك.تـطبيق]، بيانات: مؤشر) {
    // أنشئ النافذة الرئيسية.
    النافذة~مؤشر = جـتك.نـافذة_تطبيق.أنشئ(تطبيق)~مؤشر؛
    النافذة.عين_عنوانا("الأسس: دليل ودجات جتك")؛
    النافذة.عين_الحجم_المبدئي(640، 480)؛

    // أنشئ شريط الحاشية مع قائمة الأوامر وزري حول وخروج.
    النافذة.عين_شريط_عنوان(أنشئ_شريط_الحاشية())؛

    // أنشئ جانبين.
    عرف جانبان: سند[جـتك.جـانبان]؛
    جانبان~مؤشر = جـتك.جـانبان.أنشئ(جـتك.اتـجاه._أفقي_)~مؤشر؛
    النافذة.أضف(جانبان)؛
    جانبان.عين_الموقع(150)؛

    // أنشئ صندوقا للجانب البعيد.
    عرف صندوق: سند[جـتك.صـندوق]؛
    صندوق~مؤشر = جـتك.صـندوق.أنشئ(جـتك.اتـجاه._عمودي_، 10)~مؤشر؛
    جانبان.احشد2(صندوق، 1، 1)؛

    // املأ الجانب البعيد.
    عرف لصيقة: سند[جـتك.لـصيقة]؛
    عرف صوان_نصي: سند[جـتك.صـوان_نصي]؛
    لصيقة~مؤشر = أنشئ_الشعار_واللصيقة(صندوق)~مؤشر؛
    أنشئ_حقلا(صندوق، لصيقة)؛
    صوان_نصي~مؤشر = أنشئ_مساحة_الكتابة(صندوق)~مؤشر؛
    أنشئ_أزرار_مساحة_الكتابة(صندوق، صوان_نصي)؛
    أنشئ_الزر_الثنائي(صندوق، لصيقة)؛
    أنشئ_خانة_التأشير(صندوق، لصيقة)؛

    // املأ الجانب القريب.
    أنشئ_القائمة(جانبان، لصيقة)؛

    النافذة.أظهر_الكل()؛
  }؛

  دالة أنشئ_شريط_الحاشية () => سند[جـتك.شـريط_الحاشية] {
    عرف شريط_الحاشية: سند[جـتك.شـريط_الحاشية]؛
    شريط_الحاشية~مؤشر = جـتك.شـريط_الحاشية.أنشئ()~مؤشر؛
    شريط_الحاشية.عين_عنوانا("الأسس: دليل ودجات جتك")؛

    عرف زر_حول: سند[جـتك.الـزر]؛
    زر_حول~مؤشر = جـتك.الـزر.أنشئ_بلصيقة("حول")~مؤشر؛
    زر_حول.اربط_إشارة(جـتك.إشـارة._نقر_، جـلب.رد[قد_نقر_زر_حول]، 0، 0، 0)؛
    شريط_الحاشية.احشد_في_البداية(زر_حول)؛

    عرف زر_الخروج: سند[جـتك.الـزر]؛
    زر_الخروج~مؤشر = جـتك.الـزر.أنشئ_بلصيقة("خروج")~مؤشر؛
    زر_الخروج.اربط_إشارة(جـتك.إشـارة._نقر_، جـلب.رد[قد_نقر_زر_الخروج]، 0، 0، 0)؛
    شريط_الحاشية.احشد_في_النهاية(زر_الخروج)؛

    أنشئ_قائمة_الأوامر(شريط_الحاشية)؛

    أرجع شريط_الحاشية؛
  }؛

  دالة أنشئ_قائمة_الأوامر (شريط_الحاشية: سند[جـتك.شـريط_الحاشية]) {
    عرف قائمة_الأوامر: سند[جـتك.قـائمة_أوامر]؛
    قائمة_الأوامر~مؤشر = جـتك.قـائمة_أوامر.أنشئ()~مؤشر؛

    عرف عنصر: سند[جـتك.عـنصر_قائمة_أوامر]؛
    عنصر~مؤشر = جـتك.عـنصر_قائمة_أوامر.أنشئ("حول")~مؤشر؛
    عنصر.اربط_إشارة(جـتك.إشـارة._تفعيل_، جـلب.رد[قد_نقر_زر_حول]، 0، 0، 0)؛
    قائمة_الأوامر.ألحق(عنصر)؛
    عنصر.أظهر()؛
    عنصر~مؤشر = جـتك.عـنصر_قائمة_أوامر.أنشئ("خروج")~مؤشر؛
    عنصر.اربط_إشارة(جـتك.إشـارة._تفعيل_، جـلب.رد[قد_نقر_زر_الخروج]، 0، 0، 0)؛
    قائمة_الأوامر.ألحق(عنصر)؛
    عنصر.أظهر()؛

    عرف زر: سند[جـتك.مـطلق_القائمة]؛
    زر~مؤشر = جـتك.مـطلق_القائمة.أنشئ()~مؤشر؛
    زر.عين_منبثق(قائمة_الأوامر)؛
    شريط_الحاشية.احشد_في_النهاية(زر)؛
  }؛

  دالة أنشئ_القائمة (جانبان: سند[جـتك.جـانبان]، لصيقة: سند[جـتك.لـصيقة]) {
    عرف القائمة: سند[جـتك.قـائمة]؛
    القائمة~مؤشر = جـتك.قـائمة.أنشئ()~مؤشر؛
    جانبان.احشد1(القائمة، 0، 1)؛

    عرف محارف: مصفوفة[محرف، 100]؛
    عرف س: صحيح؛
    لكل س = 1، س <= 10، ++س {
      مـتم.نـص.عين(محارف~مؤشر، "السطر %d"، س)؛
      عرف لصيقة: سند[جـتك.لـصيقة]؛
      لصيقة~مؤشر = جـتك.لـصيقة.أنشئ(محارف~مؤشر)~مؤشر؛
      القائمة.احشر(لصيقة، -1)؛
    }؛

    القائمة.اربط_إشارة(جـتك.إشـارة._اختير_السطر_، جـلب.رد[قد_تغير_اختيار_القائمة]، لصيقة~مؤشر، 0، 0)؛
  }؛

  دالة أنشئ_الشعار_واللصيقة (صندوق: سند[جـتك.صـندوق]) => سند[جـتك.لـصيقة] {
    عرف صندوق_أفقي: سند[جـتك.صـندوق]؛
    عرف صورة: سند[جـتك.صـورة]؛
    عرف لصيقة: سند[جـتك.لـصيقة]؛
    صندوق_أفقي~مؤشر = جـتك.صـندوق.أنشئ(جـتك.اتـجاه._أفقي_، 20)~مؤشر؛
    لصيقة~مؤشر = جـتك.لـصيقة.أنشئ("السلام عليكم")~مؤشر؛
    صورة~مؤشر = جـتك.صـورة.أنشئ("logo.gif")~مؤشر؛
    صندوق_أفقي.احشد_في_البداية(لصيقة، 1، 0، 0)؛
    صندوق_أفقي.احشد_في_النهاية(صورة، 1، 0، 0)؛
    صندوق.احشد_في_البداية(صندوق_أفقي، 1، 0، 0)؛
    أرجع لصيقة؛
  }؛

  دالة أنشئ_حقلا (صندوق: سند[جـتك.صـندوق]، لصيقة: سند[جـتك.لـصيقة]) {
    عرف حقل: سند[جـتك.حـقل]؛
    حقل~مؤشر = جـتك.حـقل.أنشئ()~مؤشر؛

    صندوق.احشد_في_البداية(حقل، 1، 0، 0)؛
    حقل.اربط_إشارة(جـتك.إشـارة._تغير_، جـلب.رد[قد_تغير_الحقل]، لصيقة~مؤشر، 0، 0)؛
  }؛

  دالة أنشئ_مساحة_الكتابة (صندوق: سند[جـتك.صـندوق]) => سند[جـتك.صـوان_نصي] {
    عرف مساحة_نصية: سند[جـتك.مـساحة_نصية]؛
    عرف صوان_نصي: سند[جـتك.صـوان_نصي]؛
    صوان_نصي~مؤشر = جـتك.صـوان_نصي.أنشئ(0)~مؤشر؛
    مساحة_نصية~مؤشر = جـتك.مـساحة_نصية.أنشئ(صوان_نصي)~مؤشر؛
    مساحة_نصية.عين_نمط_اللف(جـتك.نـمط_اللف._كلمة_)؛
    صندوق.احشد_في_البداية(مساحة_نصية، 1، 1، 0)؛
    أرجع صوان_نصي؛
  }؛

  دالة أنشئ_أزرار_مساحة_الكتابة (صندوق: سند[جـتك.صـندوق]، صوان: سند[جـتك.صـوان_نصي]) {
    عرف صندوق_الأزرار: سند[جـتك.صـندوق_أزرار]؛
    عرف زر_الحوارية: سند[جـتك.الـزر]؛
    عرف زر_المحو: سند[جـتك.الـزر]؛
    صندوق_الأزرار~مؤشر = جـتك.صـندوق_أزرار.أنشئ(جـتك.اتـجاه._أفقي_)~مؤشر؛
    صندوق_الأزرار.عين_النسق(جـتك.طـراز_صندوق_الأزرار._انتشار_)؛
    زر_الحوارية~مؤشر = جـتك.الـزر.أنشئ_بلصيقة("قُلها")~مؤشر؛
    زر_الحوارية.اربط_إشارة(جـتك.إشـارة._نقر_، جـلب.رد[قد_نقر_زر_الحوارية]، صوان~مؤشر، 0، 0)؛
    صندوق_الأزرار.أضف(زر_الحوارية)؛
    زر_المحو ~مؤشر= جـتك.الـزر.أنشئ_بلصيقة("أمحُ")~مؤشر؛
    زر_المحو.اربط_إشارة(جـتك.إشـارة._نقر_، جـلب.رد[قد_نقر_زر_المحو]، صوان~مؤشر، 0، 0)؛
    صندوق_الأزرار.أضف(زر_المحو)؛
    صندوق.احشد_في_البداية(صندوق_الأزرار، 1، 0، 0)؛
  }؛

  دالة أنشئ_الزر_الثنائي (صندوق: سند[جـتك.صـندوق]، لصيقة: سند[جـتك.لـصيقة]) {
    عرف زر: سند[جـتك.الـزر_الثنائي]؛
    زر~مؤشر = جـتك.الـزر_الثنائي.أنشئ("بدّل")~مؤشر؛
    صندوق.احشد_في_البداية(زر، 1، 0، 0)؛
    زر.اربط_إشارة(جـتك.إشـارة._بدل_، جـلب.رد[قد_بدل_الزر_الثنائي]، لصيقة~مؤشر، 0، 0)؛
  }؛

  دالة أنشئ_خانة_التأشير (صندوق: سند[جـتك.صـندوق]، لصيقة: سند[جـتك.لـصيقة]) {
    عرف خانة: سند[جـتك.خـانة_تأشير]؛
    خانة~مؤشر = جـتك.خـانة_تأشير.أنشئ("أشّر")~مؤشر؛
    صندوق.احشد_في_البداية(خانة، 1، 0، 0)؛
    خانة.اربط_إشارة(جـتك.إشـارة._بدل_، جـلب.رد[قد_بدل_الزر_الثنائي]، لصيقة~مؤشر، 0، 0)؛
  }؛

  دالة قد_تغير_اختيار_القائمة (قائمة: سند[جـتك.قـائمة]، عنصر: سند[جـتك.سـلة]، لصيقة: سند[جـتك.لـصيقة]) {
    عرف محارف: مصفوفة[محرف، 100]؛
    مـتم.نـص.عين(
      محارف~مؤشر،
      "العنصر المختار: %s"،
      جـتك.لـصيقة.هات_النص(جـتك.سـلة.هات_ابن(عنصر)~مؤشر~مثل[مؤشر[جـتك.لـصيقة]]~محتوى)
    )؛
    لصيقة.عين_النص(محارف~مؤشر)؛
  }؛

  دالة قد_تغير_الحقل (قابل_للتعديل: سند[جـتك.قـابل_للتعديل]، لصيقة: سند[جـتك.لـصيقة]) {
    عرف محارف: مصفوفة[محرف، 100]؛
    مـتم.نـص.عين(محارف~مؤشر، "النص المدخل: %s"، جـتك.قـابل_للتعديل.هات_محارف(قابل_للتعديل، 0، -1))؛
    لصيقة.عين_النص(محارف~مؤشر)؛
  }؛

  دالة قد_نقر_زر_حول (ودجة: سند[جـتك.ودجـة]، معطيات: مؤشر) {
    عرف حوارية: سند[جـتك.حـوارية_رسالة]؛
    حوارية~مؤشر = جـتك.حـوارية_رسالة.أنشئ(
      النافذة، 0، جـتك.صـنف_رسالة._معلومة_، جـتك.نـوع_الزر._أغلق_، "الأسس: دليل ودجات جتك.\جبرمجة: سرمد خالد عبد الله"
    )~مؤشر؛
    حوارية.شغل()؛
    حوارية.مزق()؛
  }؛

  دالة قد_نقر_زر_الحوارية (ودجة: سند[جـتك.ودجـة]، صوان_نصي: سند[جـتك.صـوان_نصي]) {
    // هات النص من الصوان.
    عرف نص: مؤشر[مصفوفة[محرف]]؛
    عرف مكرر_البداية: جـتك.مـكرر_نصي؛
    عرف مكرر_النهاية: جـتك.مـكرر_نصي؛
    صوان_نصي.هات_مكرر_البداية(مكرر_البداية)؛
    صوان_نصي.هات_مكرر_النهاية(مكرر_النهاية)؛
    نص = صوان_نصي.هات_النص(مكرر_البداية، مكرر_النهاية، 1)؛

    عرف حوارية: سند[جـتك.حـوارية_رسالة]؛
    حوارية~مؤشر = جـتك.حـوارية_رسالة.أنشئ(
      النافذة، 0، جـتك.صـنف_رسالة._معلومة_، جـتك.نـوع_الزر._أغلق_، نص
    )~مؤشر؛
    حوارية.شغل()؛
    حوارية.مزق()؛
  }؛

  دالة قد_نقر_زر_المحو (ودجة: سند[جـتك.ودجـة]، صوان_نصي: سند[جـتك.صـوان_نصي]) {
    صوان_نصي.عين_النص(""، 0)؛
  }؛

  دالة قد_بدل_الزر_الثنائي (زر: سند[جـتك.الـزر_الثنائي]، لصيقة: سند[جـتك.لـصيقة]) {
    عرف فعال: ثنائي؛
    فعال = زر.أهو_فعال()؛
    إذا فعال لصيقة.عين_النص("الزر الآن مضغوط")
    وإلا لصيقة.عين_النص("الزر الآن غير مضغوط")؛
  }؛

  دالة قد_نقر_زر_الخروج (ودجة: سند[جـتك.ودجـة]، معطيات: مؤشر) {
    جـتك.ودجـة.مزق(النافذة)؛
  }؛
}؛

إذا الـعملية.عدد_المعطيات < 3 {
  دلـيل_الودجات.ابدأ()؛
} وإلا {
  عرف تنفيذي: مؤشر[بـناء.تـنفيذي] = بـناء.تـنفيذي.أنشئ(دلـيل_الودجات.ابدأ~شبم، الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1))؛
  بـناء.تـنفيذي.أضف_اعتمادية(تنفيذي، جـتك~شبم)؛
  إذا بـناء.تـنفيذي.أنتج(تنفيذي) {
    مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
  } وإلا {
    مـتم.طـرفية.اطبع("فشل البناء.\ج")؛
  }؛
}؛

