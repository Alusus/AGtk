import "Apm.alusus";
Apm.importFile("Alusus/Gtk");
import "Build.alusus";

def HelloWorld: module
{
    def window: ref[Gtk.AppWindow];

    @expname[main]
    func start {
        def status: Int;

        def app: ref[Gtk.App](Gtk.App.new("org.alusus.gtk-hello-world", 0));
        app.connectSignal(Gtk.Signal.ACTIVATE, Glib.cb[activate], 0, 0, 0);
        status = app.exec(0, 0);
        Glib.unref(app);
    };

    func activate (app: ref[Gtk.App], data: ptr): Void {

        window~ptr = Gtk.AppWindow.new(app)~ptr;
        window.setTitle("Alusus GTK Hello World");
        window.setDefaultSize(480, 320);

        def box: ref[Gtk.Box](Gtk.Box.new(Gtk.Orientation.VERTICAL, 20));

        def label: ref[Gtk.Label](Gtk.Label.new("Hello World!"));

        def buttonBox: ref[Gtk.ButtonBox](Gtk.ButtonBox.new(Gtk.Orientation.HORIZONTAL));
        buttonBox.setLayout(Gtk.ButtonBoxStyle.SPREAD);

        def aboutButton: ref[Gtk.Button](Gtk.Button.newWithLabel("About"));
        aboutButton.connectSignal(Gtk.Signal.CLICKED, Glib.cb[onAboutClicked], 0, 0, 0);
        buttonBox.add(aboutButton);

        def quitButton: ref[Gtk.Button](Gtk.Button.newWithLabel("Quit"));
        quitButton.connectSignal(Gtk.Signal.CLICKED, Glib.cb[onQuitClicked], 0, 0, 0);
        buttonBox.add(quitButton);

        box.packStart(label, 1, 0, 0);
        box.packStart(buttonBox, 1, 0, 0);

        window.add(box);

        window.showAll();
    };

    func onAboutClicked (widget: ref[Gtk.Widget], data: ptr): Void {
        def dialog: ref[Gtk.MessageDialog](Gtk.MessageDialog.new(
            window, 0, Gtk.MessageType.INFO, Gtk.ButtonsType.CLOSE,
            "Alusus GTK Hello World.\nauthor: Sarmad Abdullah"
        ));
        dialog.exec();
        Gtk.Widget.destroy(dialog);
    };

    func onQuitClicked (widget: ref[Gtk.Widget], data: ptr): Void {
        Gtk.Widget.destroy(window);
    };
};

if Process.argCount < 3 {
    HelloWorld.start();
} else {
    def exe:Build.Exe(HelloWorld.start~ast, Process.args~cnt(Process.argCount - 1));
    exe.addDependency(Gtk~ast);
    if exe.generate() {
        Srl.Console.print("Build complete.\n");
    } else {
        Srl.Console.print("Build failed.\n");
    };
};
